import util;

modeltype eventb uses core('http://emf.eventb.org/models/core/2014');
modeltype statemachines uses statemachines('http://soton.ac.uk/models/eventb/statemachines/2014');
	
library set_statemachines2context;

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// Constants
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

/*
 * Transforms statemachine to constants.
 */
mapping statemachines::Statemachine::statemachine2constants(in generatorID : String) : Sequence(eventb::context::Constant) {
	init {
		result := self.map statemachine2constant(generatorID)->asSequence()->
			union(self.nodes[statemachines::State].map state2constants(generatorID)->
			flatten());
	}
}

/*
 * Transforms state to constants.
 */
mapping statemachines::State::state2constants(in generatorID : String) : Sequence(eventb::context::Constant) {
	init {
		result := self.map state2constant(generatorID)->asSequence()->
			union(self.statemachines.map statemachine2constant(generatorID))->
			union(self.statemachines.nodes[statemachines::State].map state2constants(generatorID)->flatten());
	}
}


/*
 * Transforms statemachine to constant representing the null - i.e. statemachine is not in any state.
 * For root level statemachines the null is only generated if there is at least one final state.
 */
mapping statemachines::Statemachine::statemachine2constant(in generatorID : String) : eventb::context::Constant 
	when { self._refines = null and  (self.hasParentState() or self.hasFinalState())}
{
	name := self.name + _NULL;
	generated := true;
	attributes += getGeneratedAttr(generatorID);
}

/*
 * Transforms state to constant.
 */
mapping statemachines::State::state2constant(in generatorID : String) : eventb::context::Constant 
	when { self._refines = null }
{
	name := self.name;
	generated := true;
	attributes += getGeneratedAttr(generatorID);
}

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// Sets
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

/*
 * Transforms statemachine to sets.
 */
mapping statemachines::Statemachine::statemachine2sets(in generatorID : String) : Sequence(eventb::context::CarrierSet) {
	init {
		result := self.map statemachine2set(generatorID)->asSequence()->
			union(self.nodes[statemachines::State].statemachines.map statemachine2sets(generatorID)->flatten());
	}
}

/*
 * Transforms statemachine to set.
 */
mapping statemachines::Statemachine::statemachine2set(in generatorID : String) : eventb::context::CarrierSet 
	when { self._refines = null }
{
	name := self.name + _STATES;
	generated := true;
	attributes += getGeneratedAttr(generatorID);
}

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// Type axioms
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

/*
 * Transforms statemachine to type axioms.
 */
mapping statemachines::Statemachine::statemachine2typeAxioms(in generatorID : String) : Sequence(eventb::context::Axiom) {
	init {
		result := self.map statemachine2typeAxiom(generatorID)->asSequence()->
			union(self.nodes[statemachines::State].map state2typeAxioms(generatorID)->flatten());
	}
}

/*
 * Transforms state to type axioms.
 */
mapping statemachines::State::state2typeAxioms(in generatorID : String) : Sequence(eventb::context::Axiom) {
	init {
		result := self.map state2typeAxiom(generatorID)->asSequence()->
			union(self.statemachines.map statemachine2typeAxiom(generatorID))->
			union(self.statemachines.nodes[statemachines::State].map state2typeAxioms(generatorID)->flatten());
	}
}

/*
 * Transforms statemachine to type axiom for null state.
 */
mapping statemachines::Statemachine::statemachine2typeAxiom(in generatorID : String) : eventb::context::Axiom 
	when { self._refines = null and (self.hasParentState() or self.hasFinalState())}
{
	name := TYPEOF_ + self.name + _NULL;
	predicate := self.name + _NULL + B_IN + self.name + _STATES;
	generated := true;
	attributes += getGeneratedAttr(generatorID);
}

/*
 * Transforms state to type axiom.
 */
mapping statemachines::State::state2typeAxiom(in generatorID : String) : eventb::context::Axiom 
	when { self._refines = null }
{
	name := TYPEOF_ + self.name;
	predicate := self.name + B_IN + self.getStatemachine().name + _STATES;
	generated := true;
	attributes += getGeneratedAttr(generatorID);
}

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// Partition axioms
//
// Example: @distinct_states_in_idle_sm_STATES partition(idle_sm_STATES,{available},{notAvaliable},{idle_sm_NULL})
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

/*
 * Transforms statemachine to partition axioms.
 */
mapping statemachines::Statemachine::statemachine2partitionAxioms(in generatorID : String) : Sequence(eventb::context::Axiom) {
	init {
		result := self.map statemachine2partitionAxiom(generatorID)->asSequence()->
			union(self.nodes[statemachines::State].statemachines.map statemachine2partitionAxioms(generatorID)->flatten());
	}
}

/*
 * Transforms statemachine to partition axiom.
 */
mapping statemachines::Statemachine::statemachine2partitionAxiom(in generatorID : String) : eventb::context::Axiom 
	when { self._refines = null }
{
	init {
		var nullStatePartition : String := if self.hasParentState() or self.nodes[statemachines::Final]->notEmpty() then (self.name + _NULL).asSet() else null endif;
	}
	name := DISTINCT_STATES_IN_ + self.name + _STATES;
	predicate := "partition" + (self.name + _STATES + B_COM + self.getStateNamesAsSingletons()->union(nullStatePartition->asSequence())->toString(B_COM)).parenthesize();
	generated := true;
	attributes += getGeneratedAttr(generatorID);
}